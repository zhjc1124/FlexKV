/*
 * SPDX-FileCopyrightText: Copyright (c) <2025> NVIDIA CORPORATION & AFFILIATES.
 * All rights reserved. SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
#include <cuda_runtime.h>
#include <torch/extension.h>

#include "monitoring/metrics_manager.h"
#include "transfer.cuh"
#include "ce_transfer.h"

namespace flexkv {

#define FLOAT4_PTR(ptr) reinterpret_cast<float4 *>(ptr)

// ============================================================================
// Templated CUDA kernel
// ============================================================================

template <BackendType Type>
__global__ void transfer_kv_blocks_kernel(
    int num_blocks, int start_layer_id, int num_layers, int64_t *gpu_block_ids,
    GTensorHandler gpu_handler, int64_t gpu_startoff_inside_chunks,
    int64_t *cpu_block_ids, int64_t *cpu_ptr, int64_t cpu_kv_stride,
    int64_t cpu_layer_stride, int64_t cpu_block_stride,
    int64_t cpu_startoff_inside_chunks, int64_t copy_size, bool is_mla,
    bool is_host_to_device) {
  int kv_dim = is_mla ? 1 : 2;
  // Fold kv_dim into an inner loop so each warp iteration processes all KV
  // slots of one (layer, block). For non-MLA this halves the outer iteration
  // count, amortizing setup cost (division, block_ids fetch, layer_idx and
  // cpu_base compute) over 2x the useful work per warp step. For MLA
  // (kv_dim=1) it is equivalent to the original code.
  int num_chunks = num_layers * num_blocks;
  int64_t copy_size_in_float4 = copy_size * sizeof(int64_t) / sizeof(float4);

  int warp_id = threadIdx.x / 32;
  int lane_id = threadIdx.x % 32;
  int warps_per_block = blockDim.x / 32;
  int total_warps = gridDim.x * warps_per_block;

  for (int chunk_idx = blockIdx.x * warps_per_block + warp_id;
       chunk_idx < num_chunks; chunk_idx += total_warps) {
    int layer_idx = start_layer_id + chunk_idx / num_blocks;
    int block_pos = chunk_idx % num_blocks;
    int gpu_block_idx = gpu_block_ids[block_pos];
    int cpu_block_idx = cpu_block_ids[block_pos];

    int64_t *cpu_base = cpu_ptr + layer_idx * cpu_layer_stride +
                        cpu_block_idx * cpu_block_stride +
                        cpu_startoff_inside_chunks;

#pragma unroll
    for (int kv_idx = 0; kv_idx < kv_dim; kv_idx++) {
      int64_t *cpu_chunk_ptr = cpu_base + kv_idx * cpu_kv_stride;

      // Use template specialization to compute gpu pointer
      int64_t *gpu_ptr =
          ptr_at<Type>(gpu_handler, layer_idx, kv_idx, gpu_block_idx);
      int64_t *gpu_chunk_ptr =
          reinterpret_cast<int64_t *>(gpu_ptr) + gpu_startoff_inside_chunks;

      int64_t *src_chunk_ptr =
          is_host_to_device ? cpu_chunk_ptr : gpu_chunk_ptr;
      int64_t *dst_chunk_ptr =
          is_host_to_device ? gpu_chunk_ptr : cpu_chunk_ptr;

      for (int64_t idx = lane_id; idx < copy_size_in_float4; idx += 32) {
        float4 element;
        asm volatile("ld.global.nc.v4.f32 {%0,%1,%2,%3},[%4];"
                     : "=f"(element.x), "=f"(element.y), "=f"(element.z),
                       "=f"(element.w)
                     : "l"(&FLOAT4_PTR(src_chunk_ptr)[idx])
                     : "memory");
        asm volatile("st.global.cg.v4.f32 [%0],{%1,%2,%3,%4};" ::"l"(
                         &FLOAT4_PTR(dst_chunk_ptr)[idx]),
                     "f"(element.x), "f"(element.y), "f"(element.z),
                     "f"(element.w)
                     : "memory");
      }
    }
  }
}

// ============================================================================
// Main host function
// ============================================================================

template <BackendType Type>
void transfer_kv_blocks(
    int num_blocks, int start_layer_id, int num_layers, int64_t *gpu_block_ids,
    GTensorHandler gpu_tensor_handler, int64_t gpu_startoff_inside_chunks,
    int64_t *cpu_block_ids, void *cpu_ptr, int64_t cpu_kv_stride_in_bytes,
    int64_t cpu_layer_stride_in_bytes, int64_t cpu_block_stride_in_bytes,
    int64_t cpu_startoff_inside_chunks, int64_t chunk_size_in_bytes,
    cudaStream_t stream, int transfer_num_cta, bool is_host_to_device,
    bool use_ce_transfer, bool is_mla,
    int64_t gpu_block_stride_in_bytes, bool sync,
    const CETransferConfig &ce_config) {

  int block_size = 1024;
  int block_count = transfer_num_cta;

  int64_t *cpu_ptr_int64 = reinterpret_cast<int64_t *>(cpu_ptr);
  int64_t cpu_kv_stride_int64 = cpu_kv_stride_in_bytes / sizeof(int64_t);
  int64_t cpu_block_stride_int64 = cpu_block_stride_in_bytes / sizeof(int64_t);
  int64_t cpu_layer_stride_int64 = cpu_layer_stride_in_bytes / sizeof(int64_t);
  int64_t cpu_startoff_inside_chunks_int64 =
      cpu_startoff_inside_chunks / sizeof(int64_t);
  int64_t gpu_startoff_inside_chunks_int64 =
      gpu_startoff_inside_chunks / sizeof(int64_t);
  int64_t chunk_size_in_int64 = chunk_size_in_bytes / sizeof(int64_t);

  dim3 blockDim(block_size);
  dim3 gridDim(block_count);

  // CE transfer mode (Copy Engine using cudaMemcpyAsync)
  if (use_ce_transfer) {
    int kv_dim = is_mla ? 1 : 2;

    // Analyze block-id contiguity
    CEAnalysis analysis = analyze_ce_transfer(
        gpu_block_ids, cpu_block_ids, num_blocks,
        cpu_block_stride_in_bytes, chunk_size_in_bytes,
        gpu_block_stride_in_bytes);

    // path_opt_enabled: PER_BLOCK baseline when off; otherwise choose_path()
    // picks one of the five optimized strategies (see CEPath in ce_transfer.h).
    if (!ce_config.path_opt_enabled) {
      ce_transfer_per_block<Type>(
          num_blocks, start_layer_id, num_layers, kv_dim,
          gpu_block_ids, gpu_tensor_handler,
          gpu_startoff_inside_chunks_int64, cpu_block_ids, cpu_ptr_int64,
          cpu_kv_stride_int64, cpu_layer_stride_int64,
          cpu_block_stride_int64, cpu_startoff_inside_chunks_int64,
          chunk_size_in_bytes, stream, is_host_to_device);
    } else {
      // BF MLA preprocess: D2D transpose (before choose_path, not via the
      // five CEPath strategies). Triggered when BLOCKFIRST + MLA + CPU
      // non-contiguous. Covers both non-sharded (gpu_phys_contig) and
      // sharded D2H (!gpu_phys_contig): sharded D2H previously fell through
      // to choose_path -> STAGED_BLOCK (per-block memcpy), but benchmark
      // shows BF + sharded via D2D transpose is ~12.4x faster. sharded H2D
      // is unaffected (H2D always has gpu_phys_contig == true).
      if (ce_config.is_blockfirst && ce_config.is_mla &&
          !analysis.cpu_phys_contig) {
        ce_transfer_bf_d2d_transpose<Type>(
            num_blocks, start_layer_id, num_layers, kv_dim,
            gpu_block_ids, gpu_tensor_handler,
            gpu_startoff_inside_chunks_int64, cpu_block_ids, cpu_ptr_int64,
            cpu_kv_stride_int64, cpu_layer_stride_int64,
            cpu_block_stride_int64, cpu_startoff_inside_chunks_int64,
            chunk_size_in_bytes, stream, is_host_to_device, analysis,
            ce_config);
        if (sync) {
          cudaStreamSynchronize(stream);
        }
        return;
      }

      // force_path: test/benchmark override (production never sets it).
      CEPath path;
      if (ce_config.force_path >= 0) {
        TORCH_CHECK(ce_config.force_path <= 4,
                    "force_path out of range [0,4]: ", ce_config.force_path);
        path = static_cast<CEPath>(ce_config.force_path);
      } else {
        path = choose_path(analysis, ce_config, chunk_size_in_bytes);
      }

      switch (path) {
        case CEPath::BULK_CONTIG:
          ce_transfer_bulk_contig<Type>(
              num_blocks, start_layer_id, num_layers, kv_dim,
              gpu_block_ids, gpu_tensor_handler,
              gpu_startoff_inside_chunks_int64, cpu_block_ids, cpu_ptr_int64,
              cpu_kv_stride_int64, cpu_layer_stride_int64,
              cpu_block_stride_int64, cpu_startoff_inside_chunks_int64,
              chunk_size_in_bytes, stream, is_host_to_device);
          break;
        case CEPath::SEGMENTED_DIRECT:
          ce_transfer_segmented_direct<Type>(
              num_blocks, start_layer_id, num_layers, kv_dim,
              gpu_block_ids, gpu_tensor_handler,
              gpu_startoff_inside_chunks_int64, cpu_block_ids, cpu_ptr_int64,
              cpu_kv_stride_int64, cpu_layer_stride_int64,
              cpu_block_stride_int64, cpu_startoff_inside_chunks_int64,
              chunk_size_in_bytes, stream, is_host_to_device, analysis,
              ce_config);
          break;
        case CEPath::STAGED_MERGE:
          ce_transfer_staged_merge<Type>(
              num_blocks, start_layer_id, num_layers, kv_dim,
              gpu_block_ids, gpu_tensor_handler,
              gpu_startoff_inside_chunks_int64, cpu_block_ids, cpu_ptr_int64,
              cpu_kv_stride_int64, cpu_layer_stride_int64,
              cpu_block_stride_int64, cpu_startoff_inside_chunks_int64,
              chunk_size_in_bytes, stream, is_host_to_device, analysis,
              ce_config);
          break;
        case CEPath::STAGED_BLOCK:
          ce_transfer_staged_block<Type>(
              num_blocks, start_layer_id, num_layers, kv_dim,
              gpu_block_ids, gpu_tensor_handler,
              gpu_startoff_inside_chunks_int64, cpu_block_ids, cpu_ptr_int64,
              cpu_kv_stride_int64, cpu_layer_stride_int64,
              cpu_block_stride_int64, cpu_startoff_inside_chunks_int64,
              chunk_size_in_bytes, stream, is_host_to_device, analysis,
              ce_config);
          break;
        case CEPath::GATHER_SCATTER:
          ce_transfer_gather_scatter<Type>(
              num_blocks, start_layer_id, num_layers, kv_dim,
              gpu_block_ids, gpu_tensor_handler,
              gpu_startoff_inside_chunks_int64, cpu_block_ids, cpu_ptr_int64,
              cpu_kv_stride_int64, cpu_layer_stride_int64,
              cpu_block_stride_int64, cpu_startoff_inside_chunks_int64,
              chunk_size_in_bytes, stream, is_host_to_device, analysis,
              ce_config);
          break;
      }
    }  // end else (path_opt_enabled)
  } else {
    // Custom kernel transfer
    transfer_kv_blocks_kernel<Type><<<gridDim, blockDim, 0, stream>>>(
        num_blocks, start_layer_id, num_layers, gpu_block_ids,
        gpu_tensor_handler, gpu_startoff_inside_chunks_int64, cpu_block_ids,
        cpu_ptr_int64, cpu_kv_stride_int64, cpu_layer_stride_int64,
        cpu_block_stride_int64, cpu_startoff_inside_chunks_int64,
        chunk_size_in_int64, is_mla, is_host_to_device);
  }
  if (sync) {
    cudaStreamSynchronize(stream);
  }
}

// Explicit template instantiations
template void transfer_kv_blocks<BackendType::VLLM>(
    int, int, int, int64_t *, GTensorHandler, int64_t, int64_t *, void *,
    int64_t, int64_t, int64_t, int64_t, int64_t, cudaStream_t, int, bool, bool,
    bool, int64_t, bool, const CETransferConfig &);

template void transfer_kv_blocks<BackendType::TRTLLM>(
    int, int, int, int64_t *, GTensorHandler, int64_t, int64_t *, void *,
    int64_t, int64_t, int64_t, int64_t, int64_t, cudaStream_t, int, bool, bool,
    bool, int64_t, bool, const CETransferConfig &);

template void transfer_kv_blocks<BackendType::SGLANG>(
    int, int, int, int64_t *, GTensorHandler, int64_t, int64_t *, void *,
    int64_t, int64_t, int64_t, int64_t, int64_t, cudaStream_t, int, bool, bool,
    bool, int64_t, bool, const CETransferConfig &);

} // namespace flexkv
